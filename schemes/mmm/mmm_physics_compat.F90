!> This module contains interstitial schemes that are specific to MMM physics.
module mmm_physics_compat
    implicit none

    private
    public :: mmm_physics_accumulate_tendencies_timestep_init
    public :: mmm_physics_accumulate_tendencies_run
    public :: compute_characteristic_grid_length_scale_init
    public :: geopotential_height_wrt_sfc_to_msl_run
contains
    !> \section arg_table_mmm_physics_accumulate_tendencies_timestep_init Argument Table
    !! \htmlinclude mmm_physics_accumulate_tendencies_timestep_init.html
    pure subroutine mmm_physics_accumulate_tendencies_timestep_init( &
            dudt, dvdt, &
            rublten, rvblten, &
            errmsg, errflg)
        use ccpp_kinds, only: kind_phys

        real(kind_phys), intent(out) :: dudt(:, :), dvdt(:, :)
        real(kind_phys), intent(out) :: rublten(:, :), rvblten(:, :)
        character(*), intent(out) :: errmsg
        integer, intent(out) :: errflg

        errmsg = ''
        errflg = 0

        ! Zero out tendencies at the beginning of each time step.

        ! Tendencies for feeding back to CAM-SIMA.
        dudt(:, :) = 0.0_kind_phys
        dvdt(:, :) = 0.0_kind_phys

        ! Tendencies generated by MMM physics.
        rublten(:, :) = 0.0_kind_phys
        rvblten(:, :) = 0.0_kind_phys
    end subroutine mmm_physics_accumulate_tendencies_timestep_init

    !> \section arg_table_mmm_physics_accumulate_tendencies_run Argument Table
    !! \htmlinclude mmm_physics_accumulate_tendencies_run.html
    pure subroutine mmm_physics_accumulate_tendencies_run( &
            dudt, dvdt, &
            rublten, rvblten, &
            errmsg, errflg)
        use ccpp_kinds, only: kind_phys

        real(kind_phys), intent(inout) :: dudt(:, :), dvdt(:, :)
        real(kind_phys), intent(inout) :: rublten(:, :), rvblten(:, :)
        character(*), intent(out) :: errmsg
        integer, intent(out) :: errflg

        errmsg = ''
        errflg = 0

        ! Accumulate tendencies for feeding back to CAM-SIMA.
        dudt(:, :) = dudt(:, :) + rublten(:, :)
        dvdt(:, :) = dvdt(:, :) + rvblten(:, :)

        ! After the accumulation, zero out tendencies generated by MMM physics so that this subroutine is idempotent.
        rublten(:, :) = 0.0_kind_phys
        rvblten(:, :) = 0.0_kind_phys
    end subroutine mmm_physics_accumulate_tendencies_run

    !> \section arg_table_compute_characteristic_grid_length_scale_init Argument Table
    !! \htmlinclude compute_characteristic_grid_length_scale_init.html
    pure subroutine compute_characteristic_grid_length_scale_init( &
            omega, rearth, dx, &
            errmsg, errflg)
        use ccpp_kinds, only: kind_phys

        real(kind_phys), intent(in) :: omega(:), rearth
        real(kind_phys), intent(out) :: dx(:)
        character(*), intent(out) :: errmsg
        integer, intent(out) :: errflg

        errmsg = ''
        errflg = 0

        ! Grid sizes do not change with time. Set them just once at model initialization for better performance.

        ! Compute grid sizes in meters. This is trivial for models with regular grids like WRF,
        ! but not so straightforward for models with unstructured grids like CAM-SIMA. Here, the square root of cell area is used.
        dx(:) = sqrt(omega(:) * (rearth ** 2))
    end subroutine compute_characteristic_grid_length_scale_init

    !> \section arg_table_geopotential_height_wrt_sfc_to_msl_run Argument Table
    !! \htmlinclude geopotential_height_wrt_sfc_to_msl_run.html
    pure subroutine geopotential_height_wrt_sfc_to_msl_run( &
            ncol, &
            gravit, phis, zmsfc, &
            zmmsl, &
            errmsg, errflg)
        use ccpp_kinds, only: kind_phys

        integer, intent(in) :: ncol
        real(kind_phys), intent(in) :: gravit, phis(:), zmsfc(:, :)
        real(kind_phys), intent(out) :: zmmsl(:, :)
        character(*), intent(out) :: errmsg
        integer, intent(out) :: errflg

        integer :: i

        errmsg = ''
        errflg = 0

        ! Convert geopotential height wrt surface to geopotential height wrt mean sea level, in accordance with
        ! its normal definition.
        do i = 1, ncol
            zmmsl(i, :) = phis(i) / gravit + zmsfc(i, :)
        end do
    end subroutine geopotential_height_wrt_sfc_to_msl_run
end module mmm_physics_compat
